## 1. Phong Lighting Model

### 1. Diffuse Term

- directional light source: 태양빛
  - 물체에 입사하는 모든 빛의 방향은 같다.
- diffuse reflection(난반사)
  - 카메라의 위치에 상관 없이 같은 색을 가진다.
- n과 l벡터 사이의 각도가 빛의 양을 결정(max(n•l,0)

![image.png](/lecture-notes/CH09/라이팅1.png)

### 2. Specular Term(정반사)

- computing the reflection vector

$$
s=n\cos\theta - l \\
s=r-n\cos\theta \\
r=2n\cos\theta - l \\
=2n(n \cdot l) - l
$$

- 정반사의 강도(intensity)

$$
(r \cdot v)^{sh}
$$

### 3. Ambient(배경광)

- 빛이 모든 방향에서 들어온다.(l과 n이 의미가 없음)
- 음영 변화가 없는 어두운 색이 나온다.

### 4. Emissive(발광체)

- 스스로 빛을 낸다.

> Phong Lighting Model은 모든 빛 모델을 합친 것

⇒diffuse와 specular만으로는 간접광을 표현하기 어려운데 ambient, emissive를 합침으로써 간접광도 함께 표현해줄 수 있다.

## 2. Lighting

- diffuse의 l은 모든 점에서 동일하게 사용을 하기 때문에 uniform으로 사용
- n, v는 rasterizer가 보간해서 주는 in으로 사용
- r은 계산 결과

⇒모든 벡터들은 world space

![image.png](/lecture-notes/CH09/라이팅2.png)

→v(view vector)는 원래 없었던 존재이고 vertex shader가 view vector를 만들어주면 그것을 래스터라이저에서 보간을 하고 보간된 결과를 fragment shader로 보내준다.

## 3. Vertex Shader

```glsl
uniform mat4 worldMat, viewMat, projMat;
uniform vec3 eyePos;

layout(location = 0) in vec3 position; // object space
layout(location = 1) in vec3 normal;
layout(location = 2) in vec3 texCoord;

out vec3 v_normal, v_view;
out vec2 v_texCoord;

void main(){
	v_normal = normalize(transpose(inverse(mat3(worldMat))) * normal); // vertex의 normal, world space
	vec3 worldPos = (worldMat * vec4(position, 1.0)).xyz; // for view vector calculation
	v_view = normalize(eyePos - worldPos); // 각 정점의 view vector
	v_texCoord = texCoord;
	gl_Position = projMat * veiwMat * vec4(worldPos, 1.0); // vertex의 position, clip space
}
```

## 4. Fragment Shader

```glsl
precision mediump float;

uniform sampler2D colorMap;
uniform vec3 matSpec, matAmbi, matEmit; // Ms, Ma, Me
uniform float matSh; // shininess
uniform vec3 srcDiff, srcSpec, srcAmbi; // Sd, Ss, Sa
uniform vec3 lightDir; // directional light vector

in vec3 v_normal, v_view;
in vec2 v_texCoord;

layout(location = 0) out vec4 fragColor;

void main(){
	// normalization: 보간 과정에서 단위벡터가 아닌 결과물이 나올 수 있음
	vec3 normal = normalize(v_normal);
	vec3 view = normalize(v_view);
	vec3 light = normalize(lightDir);

	// diffuse term
	vec3 matDiff = texture(colorMap, v_texCoord).rgb;
	vec3 diff = max(dot(normal, light), 0.0) * srcDiff * matDiff;

	// specular term
	vec3 refl = 2.0 * normal * dot(normal, light) - light;
	vec3 spec = pow(max(dot(refl, view), 0.0), matSh) * srcSpec * matSpec;

	//ambient term
	vec3 ambi = srcAmbi * matAmbi;

	fragColor = vec4(diff + spec + ambi + matEmit, 1.0);
```

→fragColor 값이 rgb 범위를 넘어서면??: 1로 그냥 고정
